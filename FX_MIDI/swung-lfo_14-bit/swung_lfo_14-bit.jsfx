desc:Swung LFO (14 bit)

/*
 * Copyright (C) 2020 Evan Shimoniak
 * 
 * This program is free software: you can redistribute it and/or modify
 * it under the terms of the GNU General Public License as published by
 * the Free Software Foundation, either version 3 of the License, or
 * (at your option) any later version.
 * 
 * This program is distributed in the hope that it will be useful,
 * but WITHOUT ANY WARRANTY; without even the implied warranty of
 * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
 * GNU General Public License for more details.
 * 
 * You should have received a copy of the GNU General Public License
 * along with this program.  If not, see <https://www.gnu.org/licenses/>.
*/

slider1:3<0,31,1>MIDI CC (MSB)
slider2:67<0,100>Swing Ratio
slider3:2<1,16,1>Swing On Nth Notes
slider4:4<1,16,1>Cycles per Long Note
slider5:4<1,16,1>Cycles per Short Note
slider6:0<0,1>Phase
slider7:0<0,5,1{Sine,Asc. Saw,Desc. Saw,Square,Triangle}>Waveform

in_pin:none
out_pin:none

@init
phase = 0;
last_send = 0;

function mod(num, divisor) (
  num - (divisor * floor(num / divisor));
);

function send_lfo() (
  slider7 == 0 ? (
    new_send = (0x3FFF * (cos(2 * $pi * (phase + slider6)) + 1) / 2) & 0x3FFF;
  ) : slider7 == 1 ? (
    new_send = (0x3FFF * mod(phase + slider6, 1)) & 0x3FFF;
  ) : slider7 == 2 ? (
    new_send = (0x3FFF * (1 - mod((phase + slider6), 1))) & 0x3FFF;
  ) : slider7 == 3 ? (
    new_send = mod(phase + slider6, 1) < 0.5 ? 0x3FFF : 0;
  ) : slider7 == 4 ? (
    new_send = (
      0x3FFF * (
        mod(phase + slider6, 1) < 0.5 ? (
          2 * mod(phase + slider6, 1)
        ) : (
          2 * (1 - mod((phase + slider6), 1))
        )
      )
    ) & 0x3FFF;
  );
  new_send != last_send ? (
    midisend(
      0, 0xB0, slider1,
      new_send >> 7
    );
    midisend(
      0, 0xB0, slider1 + 32,
      new_send & 0x3FFF
    );
    last_send = new_send;
  );
);

@block
trig_slow = 2 * (ts_denom / slider3);
trig_fast = trig_slow * (slider2 / 100);
sub_beat_pos = mod(beat_position, trig_slow);

sub_beat_pos < trig_fast ? (
  phase = sub_beat_pos * slider4 / trig_fast;
);

sub_beat_pos >= trig_fast ? (
  phase = (sub_beat_pos - trig_fast) * slider5 / (trig_slow - trig_fast);
);
send_lfo();
